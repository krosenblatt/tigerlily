#!/usr/bin/perl -w
#    TigerLily:  A client for the lily CMC, written in Perl.
#    Copyright (C) 2003-2006  The TigerLily Team, <tigerlily@tlily.org>
#                                http://www.tlily.org/tigerlily/
#
#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License version 2, as published
#  by the Free Software Foundation; see the included file COPYING.
# ================================================================
#
# build_tlily - Script to build tlily executable.
#
# Usage: build_tlily  target  name=path...
#
# Builds 'target', pointing to the various paths.
#     TL_LIBDIR - Tigerlily's library module directory
#     TL_ETCDIR - Misc data files
#     TL_EXTDIR - Path to extension modules
#     TL_MODDIR - Path to tigerlily modules (??)
#
# 'lib', 'etc', 'ext', and 'mod' can be used as synonyms for the
# above, because who wants to do all that typing?


use strict;
use Config;
use Symbol;

#
# Parse command-line options.
#
my @ALLOWED_OPTIONS = qw(TL_LIBDIR TL_ETCDIR TL_EXTDIR TL_MODDIR);
my %ALLOWED_OPTIONS = map {$_ => 1} @ALLOWED_OPTIONS;
my %OPTION_SYNONYMS = (lib => 'TL_LIBDIR',
                       etc => 'TL_ETCDIR',
                       ext => 'TL_EXTDIR',
                       mod => 'TL_MODDIR',
                      );

sub usage {
    print STDERR "Usage: $0 <file to build> TL_LIBDIR=<path> TL_ETCDIR=<path>\n";
    exit 1;
}


my $file_to_build = shift @ARGV;
usage()  if (!$file_to_build  ||  $file_to_build =~ /=/);

my %opts;
for my $opt (@ARGV) {
    if ($opt =~ /^([^=]+)=(.*)/) {
        my $opt_path = $2;
        my $opt_name = $OPTION_SYNONYMS{$1} || $1;
        usage()  unless $ALLOWED_OPTIONS{$opt_name};
        $opts{$opt_name} = $opt_path;
    } else {
        usage();
    }
}

# Set option variables (with defaults)
my $TL_LIBDIR    =  $opts{TL_LIBDIR} || 'lib';
my $TL_ETCDIR    =  $opts{TL_ETCDIR} || '.';
my $TL_EXTDIR    =  $opts{TL_EXTDIR} || $TL_LIBDIR . '/extensions';
my $TL_LIBMODDIR =  $opts{TL_MODDIR} || $TL_LIBDIR;


#
# EAGAIN is nigh-impossible to determine on many systems.  Give it a try.
#

my $fallback_EAGAIN = `$^X tools/find_eagain`;
if ($fallback_EAGAIN =~ /^OK/) {
    print STDERR "Skipping hardcoded EAGAIN...\n";
    undef $fallback_EAGAIN;
} elsif ($fallback_EAGAIN =~ /^FAILED/) {
    print STDERR "Warning: Cannot determine value for EAGAIN.\n";
    undef $fallback_EAGAIN;
} elsif ($fallback_EAGAIN =~ /^(\d+)/) {
    print STDERR "Warning: Hardcoding EAGAIN as $1.\n";
    $fallback_EAGAIN = $1;
}
$fallback_EAGAIN ||= "undef";


if ($file_to_build eq "tlily") {
    my $outfile = "tlily";
    open (my $tlily, '>', $outfile)       or die "Can't write $outfile: $!\n";
    print $tlily get_script_header();
    print $tlily "use lib '$TL_LIBDIR';\n";
    open (my $template, '<', 'tlily.PL')  or die "Can't read tlily.PL: $!\n";
    print {$tlily}  <$template>;
    close $template;
    close $tlily;
    chmod(0777, $outfile);

    print qq{tlily executable built as "$outfile".\n};
    exit(0);
}

my %added;
if ($file_to_build =~ /tlily\.(.*)\.plx/) {
    my $moduleset = $1;

    my $outfile = "tlily.${moduleset}.plx";
    open (my $tlily, '>', $outfile) or die "Can't write $outfile: $!\n";
    print $tlily get_tlily_plx($moduleset);
    close $tlily;
    chmod(0777, $outfile);

    print qq{Single-file executable built as "$outfile".\n};
    exit(0);
}

die qq{Unrecognized file "$file_to_build"\n};


sub get_tlily_plx {
    my ($moduleset) = @_;

    # Override some settings.
    $TL_EXTDIR    = "//INTERNAL/extensions";
    $TL_LIBDIR    = "//INTERNAL/lib";
    $TL_LIBMODDIR = "//INTERNAL";

    my $script = get_script_header();

    my @modules = qw(
        TLily::Version
        TLily::ExoSafe
        TLily::Config
        TLily::Registrar
        TLily::Event
        TLily::Utils
        TLily::User
        TLily::UI::Util
        TLily::UI
        TLily::FoiledAgain
        TLily::UI::TextWindow::Generic
        TLily::UI::TextWindow::Input
        TLily::UI::TextWindow::StatusLine
        TLily::UI::TextWindow::Text
        TLily::UI::TextWindow
        TLily::Extend
        TLily::Server
        TLily::Server::AIM
        TLily::Server::IRC::Driver
        TLily::Server::IRC
        TLily::Server::SLCP
        TLily::Server::HTTP
        TLily::Daemon
        TLily::Daemon::Connection
        TLily::Daemon::HTTP
        TLily::Bot
    );

    if ($moduleset eq "curses") {
        push @modules, qw(TLily::FoiledAgain::Curses);
    } elsif ($moduleset eq "win32") {
        push @modules, qw(TLily::FoiledAgain::Win32);
    } elsif ($moduleset eq "tk") {
        push @modules, qw(TLily::UI::Tk::Event TLily::UI::Tk);
    }

    for my $module (@modules) {
        $added{$module} = 1;

        my $file = "lib/${module}.pm";
        $file =~ s|::|/|g;

        $script .= "{\n";
        $script .= "#### EMBEDDED \"$file\"\n";
        $script .= embedperl($file);
        $script .= "#### END\n";
        $script .= "}\n";
    }

    $script .= "{\n";
    $script .= "package main;\n";
    $script .= embedperl("tlily.PL", 1);
    $script .= "}\n";

    $script .= "__END__\n";

    for my $f (<extensions/*.pl>) {
        #next unless ($f =~ /^extensions\/(.*\.pl$/);
        #my $name = $1;

        $script .= "#### EMBEDDED \"$f\"\n";
        $script .= embedperl($f);
        $script .= "#### END\n";
    }

    $script .= "#### EMBEDDED \"lib/tlily.global\"\n";
    $script .= embedperl("lib/tlily.global");
    $script .= "#### END\n";

    return $script;
}



sub embedperl {
    my($file) = @_;

    my $fh = gensym;
    open($fh, $file) or die "$file: $!\n";

    my $script = "#line 0 $file\n";

    my $inpod = 0;
    while (<$fh>) {
        if (/^__(?:END|DATA)__/) {
            last;
        }
        if ($inpod) {
            if (/^=cut/) {
                $inpod = 0;
            }
            $script .= $_;
        } else {
            if (/^=/) {
                $inpod = 1;
                $script .= $_;
            } elsif (/^use\s+(TLily::[^\s;\(]+)(.*)/) {
                print "$file depends on $1\n" unless $added{$1};
                $script .= "BEGIN { import $1$2 }\n";
            } elsif (/^\@(?:ISA|EXPORT|EXPORT_OK|EXPORT_TAGS)\s*=/) {
                $script .= "BEGIN {\n    $_}\n";
            } elsif (/eval "require \$UI_CLASS;";/) {
                $script .= '    eval "import $UI_CLASS;";';
            } else {
                $script .= $_;
            }
        }
    }

    return $script;
}



sub get_script_header {

    my $PERL = $Config{'startperl'} ? $Config{'startperl'}     :
               $Config{'perlpath'}  ? "#!".$Config{'perlpath'} :
                                      "#/usr/bin/perl";
    my $script = <<END;
$PERL

##########################################################################
##########################################################################
###
### Tigerlily:  A client for the lily CMC, written in Perl.
### Copyright (C) 1999-2011  The TigerLily Team, <tigerlily\@tlily.org>
###                             http://www.tlily.org/tigerlily/
###
### This program is free software; you can redistribute it and/or modify it
### under the terms of the GNU General Public License version 2, as published
### by the Free Software Foundation; see the included file COPYING.
###
##########################################################################
##########################################################################


##########################################################################
### Configuration section
###

# Tigerlily will look for the "tlily.site" file in this directory.
\$::TL_ETCDIR = "$TL_ETCDIR";

# Tigerlily will load extensions from this directory.
\$::TL_EXTDIR = "$TL_EXTDIR";

# Tigerlily library directory.
\$::TL_LIBDIR = "$TL_LIBDIR";

# Tigerlily modules directory.
\$::TL_LIBMODDIR = "$TL_LIBMODDIR";

# Fallback value for EAGAIN, if it cannot be determined by other means.
\$::fallback_EAGAIN = $fallback_EAGAIN;

###
### End of user-configurable data.  Do not edit below this line.
##########################################################################

END

    return $script;
}
